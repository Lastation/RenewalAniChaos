import PluginVariables as msqcvar;
import variable as v;
import func.trig as trg;

import func.sound as s;

function main(playerID)
{
   if (v.P_CountMain[playerID] == 1)
   {
      MoveLocation("25.Milim_Bozo", v.P_UnitID[playerID], playerID, "Anywhere");
   }

   trg.Debuff_Stop();
   trg.Debuff_BanReturn();

   if (v.P_WaitMain[playerID] == 0)
   {
      if (v.P_CountMain[playerID] == 0)
      {
         if (v.P_LoopMain[playerID] == 0)
         {
            CreateUnit(1, "Flame Blue", "[Skill]Unit_Wait_8", playerID);  
            SetInvincibility(Enable, "Any unit", playerID, "[Skill]Unit_Wait_ALL");
            MoveLocation(v.P_LocationID[playerID], v.P_UnitID[playerID], playerID, "Anywhere");
            MoveUnit(All, "Flame Blue", playerID, "Anywhere", v.P_LocationID[playerID]);
            MoveLocation("25.Milim_Bozo", "Flame Blue", playerID, "Anywhere");

            trg.Main_Wait(80);

            v.P_LoopMain[playerID] += 1;
         }
         else if (v.P_LoopMain[playerID] < 60)
         {      
            var x = 50;

            if (playerID >= 3) x = -x;

            trg.Shape_Dot(playerID, 8, "40 + 1n Zealot", 0, 0);
            KillUnitAt(All, "40 + 1n Zealot", "Anywhere", playerID);

            addloc("25.Milim_Bozo", x * 3, 0);
            MoveUnit(All, "Flame Blue", playerID, "Anywhere", "25.Milim_Bozo");

            CreateUnit(3, "40 + 1n Wraith", "[Skill]Unit_Wait_8", playerID);  
            SetInvincibility(Enable, "Any unit", playerID, "[Skill]Unit_Wait_ALL");

            MoveLocation(v.P_LocationID[playerID], "Flame Blue", playerID, "Anywhere");
            MoveUnit(1, "40 + 1n Wraith", playerID, "[Skill]Unit_Wait_ALL", v.P_LocationID[playerID]);
            addloc(v.P_LocationID[playerID], -x, 0);
            MoveUnit(1, "40 + 1n Wraith", playerID, "[Skill]Unit_Wait_ALL", v.P_LocationID[playerID]);
            addloc(v.P_LocationID[playerID], -x, 0);
            MoveUnit(1, "40 + 1n Wraith", playerID, "[Skill]Unit_Wait_ALL", v.P_LocationID[playerID]);
            KillUnitAt(All, "40 + 1n Wraith", "Anywhere", playerID);

            if ((playerID >= 3 && (Bring(playerID, AtLeast, 1, "Flame Blue", "[Potal]Shop7") || Bring(playerID, AtLeast, 1, "Flame Blue", "[Potal]Potal7")))
               || (playerID < 3 && (Bring(playerID, AtLeast, 1, "Flame Blue", "[Potal]Shop8") || Bring(playerID, AtLeast, 1, "Flame Blue", "[Potal]Potal8"))))
            {
               SetDeaths(playerID, SetTo, 120, " `UniqueCoolTime");

               trg.Main_Wait(80);

               v.P_CountMain[playerID] = 2;
               v.P_LoopMain[playerID] = 0;
            }
            else if (playerID < 3)
            {
               if (Bring(P8, AtLeast, 1, "Buildings", "25.Milim_Bozo"))
               {
                  SetSwitch("Unique - MilimWarning", Set);
                  SetSwitch("Recall - Milim", Set);

                  s.CharacterVoice(3);

                  trg.Main_Wait(80);

                  v.P_CountMain[playerID] += 1;
                  v.P_LoopMain[playerID] = 0;
               }
               else
               {
                  trg.Main_Wait(80);

                  v.P_LoopMain[playerID] += 1;
               }
            }
            else if (playerID >= 3)
            {
               if (Bring(P7, AtLeast, 1, "Buildings", "25.Milim_Bozo"))
               {
                  SetSwitch("Unique - MilimWarning", Set);
                  SetSwitch("Recall - Milim", Set);

                  s.CharacterVoice(3); 

                  trg.Main_Wait(80);

                  v.P_CountMain[playerID] += 1;
                  v.P_LoopMain[playerID] = 0;
               }
               else
               {
                  trg.Main_Wait(80);

                  v.P_LoopMain[playerID] += 1;
               }
            }
         }
         else if (v.P_LoopMain[playerID] == 60)
         {      
            SetDeaths(playerID, SetTo, 120, " `UniqueCoolTime");

            trg.Main_Wait(80);
            
            v.P_CountMain[playerID] = 2;
            v.P_LoopMain[playerID] = 0;
         }
      }
      else if (v.P_CountMain[playerID] == 1)
      {
         if (v.P_LoopMain[playerID] < 40)
         {      
            trg.Shape_Edge(playerID, 1, "50 + 1n Tank", 0, 7, 120);
            if (v.P_LoopMain[playerID] % 2 == 0)
            {
               trg.Shape_Edge(playerID, 1, "Protoss Dark Archon", 0, 3, 40);
            }
            else if (v.P_LoopMain[playerID] % 2 == 1)
            {
               trg.Shape_Edge(playerID, 1, "Protoss Dark Archon", 0, 5, 80);
            }
            KillUnitAt(All, "Protoss Dark Archon", "Anywhere", playerID);
            KillUnitAt(All, "50 + 1n Tank", "Anywhere", playerID);
            KillUnitAt(All, "60 + 1n High Templar", "Anywhere", playerID);
            KillUnitAt(All, "Rhynadon (Badlands)", "Anywhere", playerID);

            trg.Main_Wait(80);

            v.P_LoopMain[playerID] += 1;
         }
         else if (v.P_LoopMain[playerID] == 40)
         {      
            MoveLocation("25.Milim_Bozo", "Flame Blue", playerID, "Anywhere");

            if (Deaths(CurrentPlayer, Exactly, 0, (210)))
            {
               MoveUnit(All, v.P_UnitID[playerID], playerID, "Anywhere", "25.Milim_Bozo");
               CenterView("25.Milim_Bozo");
            }

            trg.Shape_NxNSquare(playerID, 1, "130 + 1n Norad", 3, 75);
            trg.Shape_Dot(playerID, 16, "80 + 1n Goliath", 0, 0);
            Order("130 + 1n Norad", playerID, "Anywhere", Attack, "Anywhere");
            MoveUnit(All, "80 + 1n Goliath", playerID, "[Skill]Unit_Wait_ALL", v.P_LocationID[playerID]);
            Order("80 + 1n Goliath", playerID, "Anywhere", Attack, "Anywhere");
            SetSwitch("Recall - Milim", Clear);

            trg.Main_Wait(80);

            v.P_LoopMain[playerID] += 1;
         }
         else if (v.P_LoopMain[playerID] < 45)
         {      
            var i = v.P_LoopMain[playerID] - 41;

            trg.Shape_Edge(playerID, 1, "60 + 1n Siege", 0, 5 + 2 * i, 100 + 50 * i);
            trg.Shape_Edge(playerID, 1, "50 + 1n Battlecruiser", 0, 3 + 2 * i, 50 + 50 * i);
            KillUnitAt(All, "60 + 1n Siege", "Anywhere", playerID);
            KillUnitAt(All, "50 + 1n Battlecruiser", "Anywhere", playerID);

            trg.Main_Wait(80);

            v.P_LoopMain[playerID] += 1;
         }
         else if (v.P_LoopMain[playerID] == 45)
         {      
            KillUnitAt(All, "130 + 1n Norad", "Anywhere", playerID);
            KillUnitAt(All, "80 + 1n Goliath", "Anywhere", playerID);

            trg.Shape_Edge(playerID, 1, " Unit. Hoffnung 25000", 0, 3, 50);
            trg.Shape_Edge(playerID, 1, " Unit. Hoffnung 25000", 0, 5, 100);
            trg.Shape_Edge(playerID, 1, " Unit. Hoffnung 25000", 0, 7, 150);
            trg.Shape_Edge(playerID, 1, " Unit. Hoffnung 25000", 0, 9, 150);
            KillUnitAt(All, " Unit. Hoffnung 25000", "Anywhere", playerID);

            trg.Main_Wait(80);

            v.P_LoopMain[playerID] += 1;
         }
         else if (v.P_LoopMain[playerID] == 46)
         {     
            s.CharacterVoice(4); 
            SetSwitch("Unique - Milim", Set);
            SetDeaths(playerID, SetTo, 1440, " `UniqueCoolTime");
            SetDeaths(playerID, SetTo, 300, " `UniqueSkill");

            trg.Main_Wait(80);

            v.P_CountMain[playerID] += 1;
            v.P_LoopMain[playerID] = 0;
         }
      }
      else if (v.P_CountMain[playerID] == 2)
      {
         RemoveUnitAt(All, "Flame Blue", "Anywhere", playerID);
         SetSwitch("Unique - MilimWarning", Clear);
         trg.SkillEnd();
      }

   }
}